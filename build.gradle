buildscript {
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
        classpath 'de.sebastianboegl.gradle.plugins:shadow-log4j-transformer:1.0.1'
    }
}

plugins {
    id 'java'
    id 'org.hidetake.ssh' version '2.10.1'
}

apply plugin: 'com.github.johnrengelman.shadow'

ssh.settings {
    dryRun = project.hasProperty('dryRun')
    knownHosts = allowAnyHosts
}

version '2.0'

sourceCompatibility = 1.8

remotes {
    princessPi {
        host = '192.168.0.101'
        user = 'pi'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
    princessPicrate {
        host = '192.168.0.103'
        user = 'pi'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

//create a single Jar with all dependencies
shadowJar {
    manifest {
        attributes 'Implementation-Title': 'IA',
                'Implementation-Version': version,
                'Main-Class': 'core.Main'
    }
    baseName = project.name
    transform(de.sebastianboegl.gradle.plugins.shadow.transformers.Log4j2PluginsFileTransformer)
}

// SSH deploy
task deployPrincess {
    doLast {
        ssh.run {
            session(remotes.princessPi) {
                put from: 'build/libs/esialrobotik.ia-2.0-all.jar', into: 'IA'
                put from: 'config.json', into: 'IA'
                put from: 'configCollection.json', into: 'IA'
                put from: 'table.json', into: 'IA'
                put from: 'table0.tbl', into: 'IA'
                put from: 'table3000.tbl', into: 'IA'
                put from: 'src/main/java/actions/a2020/actions/', into: 'IA'
                execute 'sudo sync'
            }
        }
    }
}

task deployPrincessPicrate {
    doLast {
        ssh.run {
            session(remotes.princessPicrate) {
                put from: 'build/libs/esialrobotik.ia-2.0-all.jar', into: 'IA'
                put from: 'config_PMI.json', into: 'IA/config.json'
                put from: 'configCollectionPmi.json', into: 'IA'
                put from: 'table.json', into: 'IA'
                put from: 'table0.tbl', into: 'IA'
                put from: 'table3000.tbl', into: 'IA'
                put from: 'src/main/java/actions/a2020/actions/', into: 'IA'
                execute 'sudo sync'
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // Log4J : https://logging.apache.org/log4j/2.x/
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'

    // Pi4J : https://pi4j.com/1.2/index.html
    compile 'com.pi4j:pi4j-core:1.2'

    // PiCam : https://github.com/caprica/picam
    compile group: 'uk.co.caprica', name: 'picam', version: '2.0.2'

    // OpenCv
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '4.0.1-1.4.4'

    // GSon
    compile 'com.google.code.gson:gson:2.8.6'

    // RXTX
    compile group: 'org.rxtx', name: 'rxtx', version: '2.1.7'

    // Lib de WebSocket
    compile "org.java-websocket:Java-WebSocket:1.5.1"
}
